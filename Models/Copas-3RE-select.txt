data
{
  ESS.max = max(ESS)
  ESS.min = min(ESS)
  rootESS.max = pow(ESS.max, 1/2)
  rootESS.min = pow(ESS.min, 1/2)
}
model
{
  for(i in 1:S){
  
    # reparameterization, pi[i,1] = sens = P(RF | event)
    #                     pi[i,2] = 1 - spec = P(RF | no event)
    # y[i,] holds the number of "positive tests"
    y[i,1] ~ dbin(pi[i,1], n[i,1])
    y[i,2] ~ dbin(pi[i,2], n[i,2])
    
    # probability of exposure
    n[i,1] ~ dbin(psi[i], n.tot[i])
    
    psi[i] <- 1 / (1 + exp(-nu[i]))
    
    # conditional prob of event given exposure
    logit(pi[i,1]) <- beta[i] + delta[i]/2
    logit(pi[i,2]) <- beta[i] - delta[i]/2
    
    z[i] ~ dnorm(u[i], 1) T(0,)
    u[i] = gamma0 + gamma1 * pow(ESS[i], 1/2)
    
    delta[i] ~ dnorm(mean.delta[i], D.delta)
    
    mean.delta[i] <- delta0 + rho.delta.z * sigma.delta * (z[i] - u[i])
    
    nu[i] ~ dnorm(nu0, D.nu)
    
    beta[i] ~ dnorm(beta0, D.beta)
    
  }
  
  beta0 ~ dnorm(a, b)
  delta0 ~ dnorm(c, d)
  nu0 ~ dnorm(e, f)
  
  var.delta <- pow(sigma.delta, 2) * (1 - pow(rho.delta.z, 2))
  D.delta <- pow(var.delta, -2)
  D.beta <- pow(sigma.beta, -2)
  D.nu <- pow(sigma.nu, -2)
  
  # half-t prior on sigma.delta
  sigma.beta ~ dt(0, B.beta, 1) T(0,)
  sigma.nu ~ dt(0, B.nu, 1) T(0,)
  sigma.delta ~ dt(0, B.delta, 1) T(0,)
  
  rho.delta.z <- rho.spike * rho.slab
  rho.slab ~ dnorm(0.5, 1) T(-1, 1)
  rho.spike ~ dbern(0.8)
  #rho.delta.z ~ dunif(-1, 1)
  
  #gamma0 ~ dunif(-2, 2)
  #gamma1 ~ dunif(0, 1 / rootESS.min)
  gamma0 <- -0.3
  gamma1 <- 0.03
  
}